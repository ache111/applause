grammar org.applause.lang.ApplauseDsl with org.eclipse.xtext.common.Terminals

generate applauseDsl "http://www.applause.org/lang/ApplauseDsl"

// -----------------------------------------
// General Model Structure
// -----------------------------------------

Model:
	elements += AbstractElement*
;

NamespaceDeclaration:
	'namespace' name=QualifiedName '{'
		elements+=AbstractElement*
	'}'
;

AbstractElement:
	NamespaceDeclaration | Type | Platform
;
	
// -----------------------------------------
// Entities
// -----------------------------------------
	
Type:
	DataType | Entity
;

DataType:
	'datatype' name=ID
;

Entity:
	(abstract ?= 'abstract')? 'entity' name=ID ('extends' superEntity = [Entity])? '{'
		attributes += Attribute*
	'}'
;

Attribute:
	type=[Type] name=ID
;

// -----------------------------------------
// Platform specific mappings
// -----------------------------------------

Platform:
	'platform' name=ID '{'
		mappings += TypeMapping*
	'}'
;

QualifiedName:
	ID ('.' ID)*
;

TypeMapping:
	'typemapping' type=[DataType] '->' name=QualifiedName 
;