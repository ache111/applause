grammar org.applause.util.xcode.projectfile.Pbxproj hidden(WS, QUOTES, COMMENT)

generate pbxproj "http://www.applause.org/util/xcode/projectfile/Pbxproj"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ProjectModel:
	'//' '!$*' encoding=Encoding '*$!'
	'{'
		'archiveVersion' '=' archiveVersion=INT ';'
		'classes' '=' '{' '}' ';'
		'objectVersion' '=' objectVersion=INT ';'
		'objects' '=' '{'
			objects+=ProjectObject*
		'}' ';'
		'rootObject' '=' rootObject=[ProjectObject] ';'
	'}'
;

enum Encoding:
	UTF8
;

ProjectObject:
	BuildFile | FileReference | Project | Group | VariantGroup | ContainerItemProxy | FrameworksBuildPhase
;

BuildFile:
	name=UUID '=' '{' 
		'isa' '=' isa=PBXBuildFileLiteral ';'
		'fileRef' '=' fileRef=[ProjectObject] ';'
	 '}' ';'
;

PBXBuildFileLiteral:
	'PBXBuildFile'	
;

FileReference:
	name=UUID '=' '{' 
		'isa' '=' isa=PBXFileReferenceLiteral ';'
		( ('lastKnownFileType' '=' lastKnownFileType=FileType) | ('explicitFileType' '=' explicitFileType=FileType) )';'
		('includeInIndex' '=' includeInIndex=INT';')?
		('name' '=' fileName=ID ';')?
		'path' '=' path=Path ';'
		'sourceTree' '=' sourceTree=SourceTree ';'
	'}' ';'
;

PBXFileReferenceLiteral:
	'PBXFileReference'
;

enum FileType:
	wrapperApplication='wrapper.application' 
	| wrapperFramework='wrapper.framework'
	| wrapperCFBundle='wrapper.cfbundle'
	| textPlistXml='text.plist.xml'
	| testPlistStrings='text.plist.strings'
	| sourcecodeCObjc='sourcecode.c.objc'
	| sourcecodeCH='sourcecode.c.h'
;

Path:
	{Path} '/'? fragments+=PathFragment ('/' fragments+=PathFragment)*
;

PathFragment:
	PathVariable | PathID | PathString
;

PathVariable:
	'$' '('? value=SourceTree ')'?
;

PathID:
	value=ID
;

PathString:
	value=STRING
;

PathList:
	{PathList}
	'('
		('\\"'? paths+=Path '\\"'? ',')+
	')'
;

enum SourceTree:
	SDKROOT | BUILT_PRODUCTS_DIR | group='"<group>"' | inherited | SRCROOT | DEVELOPER_DIR
;

Project:
	name=UUID '=' '{'
		'isa' '=' isa=PBXProjectLiteral ';'
		'attributes' '=' '{'
			'LastUpgradeCheck' '=' lastupgradeCheck=INT ';'
			'ORGANIZATIONNAME' '=' organisationName=STRING ';'
		'}' ';'
		'buildConfigurationList' '=' buildConfigurationList=UUID ';'
		'compatibilityVersion' '=' compatibilityVersion=STRING ';'
		'developmentRegion' '=' developmentRegion=Language ';'
		'hasScannedForEncodings' '=' hasScannedForEncodings=INT ';'
		'knownRegions' '=' '('
			(knownRegions += ID ',')*
		')' ';'
		'mainGroup' '=' mainGroup=UUID ';' // TODO convert this into a reference!!!
		'productRefGroup' '=' productRefGroup=UUID ';' // TODO convert this into a reference!!!
		'projectDirPath' '=' projectDirPath=STRING ';'
		'projectRoot' '=' projectRoot=STRING ';'
		'targets' '=' '('
			(targets+=UUID ',')* // TODO reference!!!
		')' ';'
	'}' ';'
;

enum Language:
	English
;

PBXProjectLiteral:
	'PBXProject'
;

Group:
	name=UUID '=' '{'
		'isa' '=' isa=PBXGroupLiteral ';'
		'children' '=' '('
			(children+=[ProjectObject] ',')*
		')' ';'
		('name' '=' groupName=(ID | STRING) ';' )?
		('path' '=' path=Path ';')?
		'sourceTree' '=' sourceTree=SourceTree ';'
	'}' ';'
;

PBXGroupLiteral:
	'PBXGroup'
;

VariantGroup:
	name=UUID '=' '{'
		'isa' '=' isa=PBXVariantGroupLiteral ';'
		'children' '=' '('
			(children+=[ProjectObject] ',')*
		')' ';'
		('name' '=' groupName=(ID | STRING) ';' )?		
		'sourceTree' '=' sourceTree=SourceTree ';'		
	'}' ';'
;

PBXVariantGroupLiteral:
	'PBXVariantGroup'
;

ContainerItemProxy:
	name=UUID '=' '{'
		'isa' '=' isa=PBXContainerItemProxyLiteral ';'
		'containerPortal' '=' containerPortal=[Project] ';'
		'proxyType' '=' proxyType=INT ';'
		'remoteGlobalIDString' '=' remoteGlobalIDString=UUID ';'
		'remoteInfo' '=' remoteInfo=ID ';'
	'}' ';'
;

PBXContainerItemProxyLiteral:
	'PBXContainerItemProxy'
;

FrameworksBuildPhase:
	name=UUID '=' '{'
		'isa' '=' isa=PBXFrameworksBuildPhaseLiteral ';'
		'buildActionMask' '=' buildActionMask=INT ';'
		'files' '=' '('
			(files+=[BuildFile] ',')*
		')' ';'
		'runOnlyForDeploymentPostprocessing' '=' runOnlyForDeploymentPostprocessing=INT ';'
	'}' ';'
;

PBXFrameworksBuildPhaseLiteral:
	'PBXFrameworksBuildPhase'
;


UUID:
	(INT | ID)*
;

terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ID: ('a'..'z'|'A'..'Z'|'0'..'9') ('a'..'z'|'A'..'Z'|'0'..'9'|'_'|'.' | '+' | '-' | '@')*;
terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 


terminal WS: (' '|'\t'|'\r'|'\n')+;
terminal QUOTES: '"';

terminal COMMENT	: '/*' -> '*/';