grammar org.applause.lang.ApplauseDsl with org.eclipse.xtext.common.Terminals

generate applauseDsl "http://www.applause.org/lang/ApplauseDsl"

// -----------------------------------------
// General Model Structure
// -----------------------------------------

Model:
	elements += AbstractElement*
;

NamespaceDeclaration:
	'namespace' name=QualifiedName '{'
		elements+=NamespacedElement*
	'}'
;

Import:
	'uses' importedNamespace=QualifiedNameWithWildcard
;

AbstractElement:
	NamespaceDeclaration | NamespacedElement
;

NamespacedElement:
	NamedElement | Import
;

NamedElement:
	Type | Platform
;

	
// -----------------------------------------
// Entities
// -----------------------------------------
	
Type:
	DataType | Entity
;

DataType:
	'datatype' name=ID
;

Entity:
	(abstract ?= 'abstract')? 'entity' name=ID ('extends' superEntity = [Entity|QualifiedName])? '{'
		attributes += Attribute*
	'}'
;

Attribute:
	type=[Type|QualifiedName] name=ID
;

// -----------------------------------------
// Platform specific mappings
// -----------------------------------------

Platform:
	'platform' name=ID '{'
		mappings += TypeMapping*
	'}'
;

QualifiedName:
	ID ('.' ID)*
;

QualifiedNameWithWildcard:
	QualifiedName '.*'?
;

TypeMapping:
	'typemapping' type=[DataType|QualifiedName] '->' name=QualifiedName 
;